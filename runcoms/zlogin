#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
(
  # Function to determine the need of a zcompile. If the .zwc file
  # does not exist, or the base file is newer, we need to compile.
  # These jobs are asynchronous, and will not impact the interactive shell
  zcompare() {
    if [[ -s ${1} && ( ! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]]; then
      zcompile ${1}
    fi
  }

  # Compile the completion dump to increase startup speed.
  zcompare ${ZDOTDIR:-$HOME}/.zcompdump

  # zcompile .zshrc
  zcompare ${ZDOTDIR:-$HOME}/.zshrc

  # zcompile some light module init scripts
  #zcompare ${ZDOTDIR:-$HOME}/.zprezto/modules/git/init.zsh
  #zcompare ${ZDOTDIR:-$HOME}/.zprezto/modules/git/alias.zsh
  zcompare ${ZDOTDIR:-$HOME}/.zprezto/modules/utility/init.zsh
  zcompare ${ZDOTDIR:-$HOME}/.zprezto/modules/terminal/init.zsh
  zcompare ${ZDOTDIR:-$HOME}/.zprezto/modules/completion/init.zsh

  # zcompile all autoloaded functions in enabled modules
  mods=(environment terminal editor history directory completion utility ssh prompt)
  for module in $mods; do
    if [ -d "${ZDOTDIR:-$HOME}/.zprezto/modules/$module/functions/" ]; then
      for file in ${ZDOTDIR:-$HOME}/.zprezto/modules/$module/functions/^(*.zwc)(.); do
        zcompare ${file}
      done
    fi
  done

) &!

# Execute code only if STDERR is bound to a TTY.
[[ -o INTERACTIVE && -t 2 ]] && {

  # Print a random, hopefully interesting, adage.
  if (( $+commands[fortune] )); then
    fortune -s
    print
  fi

} >&2
