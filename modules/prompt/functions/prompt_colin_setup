#
# Colin's colourful theme with Git support
#
# Author:
#	Colin Seymour <lildood@gmail.com>
#

function prompt_colin_precmd {
	if [[ -z $(git ls-files --other --exclude-standard 2> /dev/null) ]] {
        zstyle ':vcs_info:*' formats '%F{magenta} (%F{yellow}%b%c%u%F{green}%F{magenta})'
    } else {
        zstyle ':vcs_info:*' formats '%F{magenta} (%F{yellow}%b%c%u%F{red}●%F{green}%F{magenta})'
    }
	vcs_info
}

function prompt_colin_setup {
	setopt LOCAL_OPTIONS
  	unsetopt XTRACE KSH_ARRAYS
  	prompt_opts=(cr percent subst)

 	# Load required functions.
  	autoload -Uz add-zsh-hook
  	autoload -Uz vcs_info

  	# Add hook for calling vcs_info before each command.
  	add-zsh-hook precmd prompt_colin_precmd

  	# Set vcs_info parameters.
  	zstyle ':vcs_info:*' stagedstr '%F{green}●'
  	zstyle ':vcs_info:*' unstagedstr '%F{yellow}●'
  	zstyle ':vcs_info:*' check-for-changes true
  	zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{11}%r'
  	zstyle ':vcs_info:*' enable git svn hg


  	# Define prompts.
  PROMPT="%F{magenta}[%(!.%F{red}.%F{blue})%m : %F{green}%40<...<%~\${vcs_info_msg_0_}%F{magenta}]%F{blue}%(!.#.$) %f"
  RPROMPT=''
}

prompt_colin_setup "$@"
